// Таблица настроек на клиенте в виде массива структур
&НаКлиенте
Перем МассивНастроек;

// -----------------------------------
// ---- Служебные процедуры и функции

// Преобразовывает имя переменной к более читабельному виду, аналогично внутренним алгоритмам 1С
// Например "ОбменСБазой" -> "Обмен с базой"
// https://infostart.ru/public/196681/
//
&НаКлиентеНаСервереБезКонтекста
Функция глозПредставлениеПеременной(знач ИмяПеременной) Экспорт
    Перем МаленькиеБуквы, Цифры, Буква, Ответ, сч, ПредыдущаяБуква, СледующаяБуква;
    ИмяПеременной = СокрЛП(ИмяПеременной);
    МаленькиеБуквы = "абвгдеёжзийклмнопрстуфхцчшщъыьэюя_";
    Цифры="0123456789.";
    Буква = Лев(ИмяПеременной, 1);
    Ответ = ""+Буква;
    Для сч=2 По СтрДлина(ИмяПеременной) Цикл
        ПредыдущаяБуква = Буква;
        Буква = Сред(ИмяПеременной, сч, 1);
        Если (Найти(МаленькиеБуквы, Буква)=0) и (Найти(МаленькиеБуквы, ПредыдущаяБуква)>0) Тогда
            СледующаяБуква = Сред(ИмяПеременной, сч+1, 1);
            Если (СледующаяБуква<>"") и (Найти(МаленькиеБуквы, СледующаяБуква)>0) Тогда
                Буква = НРег(Буква);
            КонецЕсли;
            Буква = " "+Буква;
        ИначеЕсли (Найти(Цифры, Буква)>0) и (Найти(Цифры, ПредыдущаяБуква)=0) Тогда
            Буква = " "+Буква;
        КонецЕсли;
        Ответ = Ответ + Буква;
    КонецЦикла;
    Ответ = СтрЗаменить(Ответ, "_", " ");

    //этого пока нет в релизе - убирает все задвоенные пробелы, типа "Сумма___С_НДС" --> "Сумма   с НДС"
    Пока Найти(Ответ, "  ")>0 Цикл Ответ = СтрЗаменить(Ответ, "  ", " ") КонецЦикла;
    
    Возврат Ответ;
КонецФункции

// Функция возращает дерево значний из таблицы значений, с нужной группировкой
// http://www.odincplus.com/stati-programmistam/preobrazovanie-tablitsy-znacheniy-v-derevo-znacheniy.html 
// 
&НаСервереБезКонтекста
Функция ТаблицаЗначенийВДеревоЗначений(ТЗ, ГруппировкиКолонки) Экспорт 
   ПЗ             = Новый ПостроительЗапроса;
   ПЗ.ИсточникДанных          = Новый ОписаниеИсточникаДанных(ТЗ);//передаем ТЗ

   ПЗ.ДобавлениеПредставлений = ТипДобавленияПредставлений.НеДобавлять;
   
   Для Каждого ГруппировкаКолонка из РазложитьСтрокуВМассив(ГруппировкиКолонки) Цикл
	ПЗ.ИсточникДанных.Колонки[ГруппировкаКолонка].Измерение = Истина;//по этой колонке идет группировка
   КонецЦикла;

   ПЗ.ЗаполнитьНастройки();
   ПЗ.Выполнить();
   Дерево = ПЗ.Результат.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);  
   Возврат Дерево;  
КонецФункции// ТаблицаЗначенийВДеревоЗначений

&НаКлиентеНаСервереБезКонтекста
Функция РазложитьСтрокуВМассив(ИсхСтрока, Разделитель = ",")
    
    ВремМногострочнТекст = СтрЗаменить(ИсхСтрока, Разделитель, Символы.ПС);
    МассивСтрок = Новый Массив();
    
    Для Счетчик = 1 По СтрЧислоСтрок(ВремМногострочнТекст) Цикл        
        МассивСтрок.Добавить(СокрЛП(СтрПолучитьСтроку(ВремМногострочнТекст, Счетчик)));
    КонецЦикла;
    
    Возврат МассивСтрок;
КонецФункции

&НаСервереБезКонтекста
Функция ПоискСинонимаПоЧастиСтроки(СтрокаПоиска, МассивНастроек)
	
	ТаблицаНастроек = Новый ТаблицаЗначений;
	ТаблицаНастроек.Колонки.Добавить("ИмяЭлемента", Новый ОписаниеТипов("Строка"));
	ТаблицаНастроек.Колонки.Добавить("ЗаголовокЭлемента", Новый ОписаниеТипов("Строка"));
	ТаблицаНастроек.Колонки.Добавить("ИмяФормы", Новый ОписаниеТипов("Строка"));
	ТаблицаНастроек.Колонки.Добавить("ЗаголовокФормы", Новый ОписаниеТипов("Строка"));
	
	Для Каждого ТекущаяСтрока из МассивНастроек Цикл
    	НоваяСтрока = ТаблицаНастроек.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
	КонецЦикла; 
 
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаНастроек.ИмяЭлемента,
		|	ТаблицаНастроек.ЗаголовокЭлемента,
		|	ТаблицаНастроек.ИмяФормы,
		|	ТаблицаНастроек.ЗаголовокФормы
		|ПОМЕСТИТЬ втТаблицаНастроек
		|ИЗ
		|	&ТаблицаНастроек КАК ТаблицаНастроек
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТаблицаНастроек.ИмяЭлемента,
		|	втТаблицаНастроек.ЗаголовокЭлемента,
		|	втТаблицаНастроек.ИмяФормы,
		|	втТаблицаНастроек.ЗаголовокФормы
		|ИЗ
		|	втТаблицаНастроек КАК втТаблицаНастроек
		|ГДЕ
		|	втТаблицаНастроек.ЗаголовокЭлемента ПОДОБНО &СтрокаПоиска";
	
	Запрос.УстановитьПараметр("ТаблицаНастроек", ТаблицаНастроек);
	Запрос.УстановитьПараметр("СтрокаПоиска", "%"+СтрокаПоиска+"%");
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	ТаблицаРезультата.Колонки.Добавить("ПоПервымСимволам", Новый ОписаниеТипов("Число"));
	
	Для Каждого ТекущаяСтрока из ТаблицаРезультата Цикл
		Если Найти(НРег(ТекущаяСтрока.ЗаголовокЭлемента), НРег(СтрокаПоиска)) <> 1 Тогда
			ТекущаяСтрока.ЗаголовокЭлемента = СтрокаПоиска + "_" + ТекущаяСтрока.ЗаголовокЭлемента;
			ТекущаяСтрока.ПоПервымСимволам = 1;
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаРезультата.Сортировать("ПоПервымСимволам");
	
	Результат = Новый Массив;
	Для Каждого ТекущаяСтрока из ТаблицаРезультата Цикл
		СвойстваФормы = Новый Структура;
		СвойстваФормы.Вставить("ИмяЭлемента", ТекущаяСтрока.ИмяЭлемента);
		СвойстваФормы.Вставить("ЗаголовокЭлемента", ТекущаяСтрока.ЗаголовокЭлемента);
		СвойстваФормы.Вставить("ИмяФормы", ТекущаяСтрока.ИмяФормы);
		СвойстваФормы.Вставить("ЗаголовокФормы", ТекущаяСтрока.ЗаголовокФормы);
		Результат.Добавить(СвойстваФормы);
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьМассивФормИзМетаданных()
	
	ИмяКонфигурации = Метаданные.Представление();
	
	ДляУТ = Новый Массив;
	ДляУТ.Добавить("НастройкаРазрешенийНаИспользованиеВнешнихРесурсов");
	ДляУТ.Добавить("ПанельАдминистрирования1СМаркировка");
	ДляУТ.Добавить("ПанельАдминистрированияБИД");
	ДляУТ.Добавить("ПанельАдминистрированияБИП");
	ДляУТ.Добавить("ПанельАдминистрированияБСП");
	ДляУТ.Добавить("ПанельАдминистрированияБСПВМоделиСервиса");
	ДляУТ.Добавить("ПанельАдминистрированияУТ");
	ДляУТ.Добавить("ПанельАдминистрированияЭДО");

	
	Результат = Новый Массив;
	Для Каждого Обработка из Метаданные.Обработки Цикл
		
		Если ИмяКонфигурации = "Управление торговлей, редакция 11" И ДляУТ.Найти(Обработка.Имя) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ФормаОбработки из Обработка.Формы Цикл
			СвойстваФормы = Новый Структура;
			СвойстваФормы.Вставить("ПолноеИмяФормы", "Обработка." + Обработка.Имя + ".Форма."+ФормаОбработки.Имя);
			СвойстваФормы.Вставить("СинонимФормы", ФормаОбработки.Синоним);
			Результат.Добавить(СвойстваФормы);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// ---------------------------------
// ---- Обработчики событий формы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ТаблицаНастроек.Количество() = 0 Тогда
		
		Состояние("Получение форм настроек...");
		
		ФормыНастроек = ПолучитьМассивФормИзМетаданных();
		КоличествоФорм = ФормыНастроек.Количество();
		Сч = 0;
		Для Каждого СвойстваФормы из ФормыНастроек Цикл
			Сч = Сч + 1;
			ИмяФормыНастроек = СвойстваФормы.ПолноеИмяФормы;
			Состояние("------- " + Сч + " / " + КоличествоФорм + " -------" + Символы.ПС + СвойстваФормы.ПолноеИмяФормы);

			// Только формы с основным реквизитом "НаборКонстант"
			Попытка
				Форма = ПолучитьФорму(СвойстваФормы.ПолноеИмяФормы, Новый Структура("АвтоТест, ДанныеВыбора, Объект", Истина, Новый Структура(), Новый Структура()));;
				
				ОсновнойРеквизит = Форма.НаборКонстант;
			Исключение
				продолжить;
			Конецпопытки;
		
			Для Каждого ЭлементФормы из Форма.Элементы Цикл
				Если ТипЗнч(ЭлементФормы) = Тип("ПолеФормы") ИЛИ ТипЗнч(ЭлементФормы) = Тип("КнопкаФормы") И ЭлементФормы.Вид = ВидКнопкиФормы.Гиперссылка Тогда
					//Сообщить(ЭлементФормы.Имя + " - " + ЭлементФормы.Заголовок);
					НоваяСтрока = ТаблицаНастроек.Добавить();
					НоваяСтрока.ИмяЭлемента = ЭлементФормы.Имя;
					НоваяСтрока.ЗаголовокЭлемента = ЗаголовокЭлемента(ЭлементФормы);
					НоваяСтрока.ИмяФормы = СвойстваФормы.ПолноеИмяФормы;
					НоваяСтрока.ЗаголовокФормы = СвойстваФормы.СинонимФормы;
					ОбработкаПрерыванияПользователя();
					
					ГруппаЭлемента = УстановитьГруппуЭлемента(ЭлементФормы, Ложь);
					НоваяСтрока.ЗаголовокГруппы = ЗаголовокЭлемента(ГруппаЭлемента);
				КонецЕсли;
			КонецЦикла;
			//прервать;
		КонецЦикла;
		Сообщить("Настройки загружены!");
		//СохранитьНастройкиПользователя();
	КонецЕсли;
	
	МассивНастроек = Новый Массив;
	Для Каждого ТекущаяСтрока из ТаблицаНастроек Цикл
		СвойстваЭлемента = Новый Структура;
		СвойстваЭлемента.Вставить("ИмяЭлемента", ТекущаяСтрока.ИмяЭлемента);
		СвойстваЭлемента.Вставить("ЗаголовокЭлемента", ТекущаяСтрока.ЗаголовокЭлемента);
		СвойстваЭлемента.Вставить("ИмяФормы", ТекущаяСтрока.ИмяФормы);
		СвойстваЭлемента.Вставить("ЗаголовокФормы", ТекущаяСтрока.ЗаголовокФормы);
		МассивНастроек.Добавить(СвойстваЭлемента);
	КонецЦикла;
	
	// Сформировать дерево
	//Элементы.ТаблицаНастроек.Видимость = Ложь;
	СформироватьДеревоНастроекНаСервере();
	
	//Развернуть дерево 
	Для Каждого Раздел Из ДеревоНастроек.ПолучитьЭлементы() Цикл 
		Раздел.ЗаголовокЭлемента = Раздел.ЗаголовокФормы;
		Для Каждого Группа из Раздел.ПолучитьЭлементы() Цикл
			Группа.ЗаголовокЭлемента = ?(ЗначениеЗаполнено(Группа.ЗаголовокГруппы), Группа.ЗаголовокГруппы, Раздел.ЗаголовокФормы);
		КонецЦикла;
	КонецЦикла; 

КонецПроцедуры

&НаКлиенте
Процедура ПолеПоискаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)

	МассивНайденныхСтрок = ПоискСинонимаПоЧастиСтроки(Текст, МассивНастроек);

	Элементы.ПолеПоиска.СписокВыбора.Очистить();
	Для Каждого ТекущаяСтрока из МассивНайденныхСтрок Цикл
		Значение = ТекущаяСтрока.ИмяЭлемента + " - " + ТекущаяСтрока.ИмяФормы;
		Представление = ТекущаяСтрока.ЗаголовокЭлемента + " - " + ТекущаяСтрока.ЗаголовокФормы;
		
		Элементы.ПолеПоиска.СписокВыбора.Добавить(Значение, Представление);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПолеПоискаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Разделитель = " - ";
	
	ПозицияРазделителя = Найти(ВыбранноеЗначение, Разделитель);
	Если ПозицияРазделителя > 0 Тогда
		ИмяЭлементаФормы = Лев(ВыбранноеЗначение, ПозицияРазделителя-1);
		ИмяФормыЭлемента = Сред(ВыбранноеЗначение, ПозицияРазделителя + СтрДлина(Разделитель));
		
		//Сообщить(ИмяНастройки);
		//Сообщить(ПолноеИмяФормыНастройки);
		
		ФормаНастроек = ПолучитьФорму(
			ИмяФормыЭлемента,
			Новый Структура,
			,
			ИмяФормыЭлемента + ".ОтдельноеОкно",
			);
			
		//Установим фокум на элемент и выделим красным.
			
		ЭлементФормы = ФормаНастроек.Элементы.Найти(ИмяЭлементаФормы);
		Если ЭлементФормы <> Неопределено Тогда
			Если ТипЗнч(ЭлементФормы) = Тип("ПолеФормы") Тогда
				ЭлементФормы.ЦветТекстаЗаголовка = WebЦвета.Красный;
			ИначеЕсли ТипЗнч(ЭлементФормы) = Тип("КнопкаФормы") Тогда
				ЭлементФормы.ЦветТекста = WebЦвета.Красный;
			КонецЕсли;
			
			УстановитьГруппуЭлемента(ЭлементФормы, Истина);

			ФормаНастроек.ТекущийЭлемент = ЭлементФормы;
		КонецЕсли;
		
		Элементы.ДеревоНастроек.ТекущаяСтрока = НайтиСтрокуВДереве(ДеревоНастроек.ПолучитьЭлементы(), ИмяЭлементаФормы, ИмяФормыЭлемента);
		
		ФормаНастроек.Открыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаИменНастроекВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущаяСтрока = ТаблицаНастроек.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если ТекущаяСтрока <> Неопределено Тогда
		ПолеПоискаОбработкаВыбора(, ТекущаяСтрока.ИмяЭлемента + " - " + ТекущаяСтрока.ИмяФормы, );
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоНастроекВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущаяСтрока = ДеревоНастроек.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если ТекущаяСтрока <> Неопределено Тогда
		ПолеПоискаОбработкаВыбора(, ТекущаяСтрока.ИмяЭлемента + " - " + ТекущаяСтрока.ИмяФормы, );
	КонецЕсли;
КонецПроцедуры

// -----------------------------------
// ---- Вспомогательные процедуры и функции

&НаКлиенте
Функция ЗаголовокЭлемента(ЭлементФормы)
	Если ЭлементФормы = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Результат = ЭлементФормы.Заголовок;
	
	Если ПустаяСтрока(Результат) Тогда
		Результат = глозПредставлениеПеременной(ЭлементФормы.Имя);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Функция УстановитьГруппуЭлемента(ЭлементФормы, Активизировать = Истина)
	Результат = Неопределено;
	Родитель = ЭлементФормы.Родитель;
	Пока ТипЗнч(Родитель) <> Тип("УправляемаяФорма") Цикл
		Если ТипЗнч(Родитель) = Тип("ГруппаФормы") И Родитель.Вид = ВидГруппыФормы.ОбычнаяГруппа И Родитель.Поведение = ПоведениеОбычнойГруппы.Свертываемая Тогда
			Если Активизировать Тогда
				Родитель.Поведение = ПоведениеОбычнойГруппы.Обычное;
			КонецЕсли;
			Результат = Родитель;
		КонецЕсли;
		Если ТипЗнч(Родитель) = Тип("ГруппаФормы") И Родитель.Вид = ВидГруппыФормы.Страница Тогда
			Если Активизировать Тогда
				Родитель.Родитель.ТекущаяСтраница = Родитель;
			КонецЕсли;
			Результат = Родитель;
		КонецЕсли;
		Родитель = Родитель.Родитель;
	КонецЦикла;		
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция НайтиСтрокуВДереве(КоллекцияСтрокДереваОдногоУровня, ИмяЭлемента, ИмяФормы) 
	Результат = Неопределено;
    Для Каждого ТекСтрока Из КоллекцияСтрокДереваОдногоУровня Цикл
        Если ТекСтрока.ИмяЭлемента = ИмяЭлемента И ТекСтрока.ИмяФормы = ИмяФормы Тогда
            Результат = ТекСтрока.ПолучитьИдентификатор();
        Иначе
            Результат = НайтиСтрокуВДереве(ТекСтрока.ПолучитьЭлементы(), ИмяЭлемента, ИмяФормы);
		КонецЕсли;
		Если Результат <> Неопределено Тогда
			Возврат Результат
		КонецЕсли;
    КонецЦикла;
    Возврат Неопределено;
КонецФункции

&НаСервере
Процедура СформироватьДеревоНастроекНаСервере()
	
	ДеревоЗначений = ТаблицаЗначенийВДеревоЗначений(ТаблицаНастроек.Выгрузить(), "ЗаголовокФормы, ЗаголовокГруппы");
	ЗначениеВРеквизитФормы(ДеревоЗначений, "ДеревоНастроек");

КонецПроцедуры

